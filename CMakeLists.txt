cmake_minimum_required(VERSION 3.0)
project(CaenSLC)

#Here we add a Cmake Module abble to find GSOAP and CAEN on your system
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/")

set(CMAKE_CXX_STANDARD 14)

set(CMAKE_INSTALL_DIR /opt/CaenSLC)

find_package(gsoap REQUIRED)
find_package(CAEN REQUIRED)
find_package(Boost COMPONENTS program_options REQUIRED)

#Create the directory that will host files generated by GSOAP
file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/generated) 

#Add application files and gsoap dependencies
set(SRC_CLIENT_FILES
	./src/CaenSLC_Client.cpp
	${CMAKE_BINARY_DIR}/generated/soapCaenSLCProxy.cpp	
	${CMAKE_BINARY_DIR}/generated/soapC.cpp
)

set(SRC_SERVER_FILES
	./src/CaenSLC_Server.cpp
	${CMAKE_BINARY_DIR}/generated/soapCaenSLCService.cpp
	${CMAKE_BINARY_DIR}/generated/soapC.cpp
)

#some files are generated by gsoap
set_source_files_properties( ${CMAKE_BINARY_DIR}/generated/soapCaenSLCProxy.cpp	 PROPERTIES GENERATED TRUE ) 
set_source_files_properties( ${CMAKE_BINARY_DIR}/generated/soapCaenSLCService.cpp	 PROPERTIES GENERATED TRUE ) 
set_source_files_properties( ${CMAKE_BINARY_DIR}/generated/soapC.cpp	 PROPERTIES GENERATED TRUE ) 

#include .h folders here
include_directories(
        ./include
	${CMAKE_BINARY_DIR}/generated
	${GSOAP_INCLUDE_DIR}
	${CAEN_INCLUDE_DIR}
)

#add the source files to the client executable
add_executable(CAENClient ${SRC_CLIENT_FILES} )

#add the source files to the client executable
add_executable(CAENServer ${SRC_SERVER_FILES} )

#Create a cmake target that generate gsoap files
add_custom_command(
	OUTPUT ${CMAKE_BINARY_DIR}/generated/soapCaenSLCProxy.cpp ${CMAKE_BINARY_DIR}/generated/soapCaenSLCService.cpp ${CMAKE_BINARY_DIR}/generated/soapC.cpp
	COMMAND ${GSOAP_SOAPCPP2} -d ${CMAKE_BINARY_DIR}/generated -j -L -wx ${CMAKE_SOURCE_DIR}/include/CaenSLC.h
	DEPENDS ${CMAKE_SOURCE_DIR}/include/CaenSLC.h
	COMMENT "CREATING BINDING FILES"
)
   
add_custom_target(GSOAP_GENERATION_TARGET
  DEPENDS ${CMAKE_BINARY_DIR}/generated/soapC.cpp ${CMAKE_BINARY_DIR}/generated/soapCaenSLCService.cpp ${CMAKE_BINARY_DIR}/generated/soapCaenSLCProxy.cpp)
	
#Make sure that the client is compiled only after gsoap has been processed
add_dependencies(CAENClient GSOAP_GENERATION_TARGET)
add_dependencies(CAENServer GSOAP_GENERATION_TARGET)

#extra libraries for the executable
target_link_libraries(CAENClient ${GSOAP_CXX_LIBRARIES} ${Boost_LIBRARIES} jsoncpp)
target_link_libraries(CAENServer ${GSOAP_CXX_LIBRARIES} ${CAEN_DGTZ_LIBRARIES} ${CAEN_COMM_LIBRARIES})

install(DIRECTORY ${CMAKE_SOURCE_DIR}/scripts
  DESTINATION ${CMAKE_INSTALL_DIR})

install(DIRECTORY ${CMAKE_BINARY_DIR}/generated
  DESTINATION ${CMAKE_INSTALL_DIR})

install(TARGETS CAENClient CAENServer
  DESTINATION ${CMAKE_INSTALL_DIR})
